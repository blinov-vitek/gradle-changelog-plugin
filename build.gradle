buildscript {
    repositories { jcenter() }

    dependencies {
        classpath 'ca.cutterslade.gradle:gradle-dependency-analyze:1.0.3'
    }
}

plugins {
  id "com.gradle.plugin-publish" version "0.9.3"
  id "nebula.nebula-release" version "4.0.1"
  id "maven"
  id "groovy"
}

group = 'com.emetriq.gradle'

// check dependencies
apply plugin: "ca.cutterslade.analyze"

repositories { jcenter() }

dependencies {
    compile gradleApi()
    compile 'com.netflix.nebula:nebula-release-plugin:4.0.1'
    compile 'org.ajoberstar:grgit:1.5.0'
    compile 'org.ajoberstar:gradle-git:1.4.1'

    testCompile ('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'com.netflix.nebula:nebula-test:4.0.0'
}

// For publishing the plugin

pluginBundle {
  website = 'https://github.com/emetriq/gradle-changelog-plugin'
  vcsUrl = 'https://github.com/emetriq/gradle-changelog-plugin'
  description = 'Creating a semi-automatic changelog on top of nebula release plugin'
  tags = ['changelog', 'release', 'emetriq', 'versioning']

  plugins {
    greetingsPlugin {
      id = 'emetriq.changelog-release'
      displayName = 'Changelog Release Plugin'
    }
  }
}

if (hasProperty("publishPlugin") ? publishPlugin.toBoolean() : false) {
    project.tasks.release.finalizedBy(publishPlugins)
}
